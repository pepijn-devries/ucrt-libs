# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libsodium
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.18
pkgrel=3
pkgdesc="P(ortable|ackageable) NaCl-based crypto library (mingw-w64)"
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/jedisct1/libsodium"
license=('spdx:ISC')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://download.libsodium.org/libsodium/releases/${_realname}-${pkgver}.tar.gz")
sha256sums=('6f504490b342a4f8a4c4a02fc9b866cbef8622d5df4e5452b46be121e46636c1')


prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  sed -i '/AX_ADD_FORTIFY_SOURCE/d' configure.ac
  sed -i 's/_FORTIFY_SOURCE/BLABLABLA/g' configure.ac
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  # Somehow llvm seems to default ssp
  #if [[ $MINGW_PACKAGE_PREFIX == *-clang-* ]]; then
  #  CPPFLAGS+=" -U_FORTIFY_SOURCE"
  #  CFLAGS+=" -fno-stack-protector"
  #fi

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --disable-ssp

  make
}

check() {
  cd ${srcdir}/build-${MSYSTEM}
  make -k check || true
}

package() {
  cd ${srcdir}/build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
