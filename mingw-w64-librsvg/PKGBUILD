# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=librsvg
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.50.9
pkgrel=2
pkgdesc="SVG rendering library (mingw-w64)"
arch=('any')
url="https://wiki.gnome.org/Projects/LibRsvg"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-rust"
             "setconf"
             "gtk-doc"
             "autoconf"
             "automake"
             "libtool")
depends=("${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-harfbuzz"
         "${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-libxml2")
options=('staticlibs' 'strip')
source=("https://download.gnome.org/sources/librsvg/${pkgver%.*}/${_realname}-${pkgver}.tar.xz"
        "0005-hack-unixy-paths.patch"
        "configure.patch")
sha256sums=('518905fffa879b6c7f3db1aae961cf31333e0eadc7b4cdd4f531707868c54b53'
            'b23b094c0cb65fcbbbb952350448de6f3430b30f273e05acdbf7a56d634212dc'
            'SKIP')

prepare() {
  # Hack: use external rust for now, until we have our own
  # export PATH="$(cygpath -u $USERPROFILE)/.cargo/bin:$PATH"
  # TARGET="${MINGW_CHOST/w64-mingw32/pc-windows-gnu}"

  # # The new ntdll dependency gives conflicts on win32
  # #if [[ $CARCH = "i686" ]]; then
  #   RUSTVER="1.69"
  # #else
  # #  RUSTVER="stable"
  # #fi

  # if command -v rustup; then
  #   echo "Found existing rustup!"
  #   rustup target add $TARGET
  #   rustup default "$RUSTVER-$TARGET"
  # else
  #   curl -L https://win.rustup.rs -o rustup-init.exe
  #   ./rustup-init.exe -yv --default-toolchain stable --default-host $TARGET
  # fi

  echo "PATH=$PATH"
  rustc --version

  # Actual patches
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0005-hack-unixy-paths.patch"
  patch -p1 -i "${srcdir}/configure.patch"
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -rf ${srcdir}/${_realname}-${pkgver} "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  CXXFLAGS+=" -D_POSIX_SOURCE"
  CFLAGS+=" -D_POSIX_SOURCE"

  # an argument passed to glib-mkenums starts with a c++ comment
  export MSYS2_ARG_CONV_EXCL="/*"
  LIBS="-lgdi32 -lgdiplus -ldnsapi -liphlpapi -lbcrypt -lntdll" \
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --disable-shared \
    --disable-introspection \
    --enable-tools=yes \
    --enable-pixbuf-loader=yes \
    --enable-gtk-doc=no \
    --enable-gtk-doc-html=no \
    --enable-silent-rules

  # pass static rust package to linker
  sed -i "s/^deplibs_check_method=.*/deplibs_check_method=\"pass_all\"/g" libtool
  # add missing crt libs (ws2_32 and userenv) to LIBRSVG_LIBS
  sed -i "s/^LIBRSVG_LIBS = .*/& -lws2_32 -luserenv/g" Makefile
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install || true
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING.LIB" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LIB"
}
